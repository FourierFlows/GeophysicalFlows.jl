module TwoDNavierStokes

export
  Problem,
  set_Î¶!,
  updatevars!,

  energy,
  energy_dissipation_hyperviscosity,
  energy_dissipation_hypoviscosity,
  energy_work,
  enstrophy,
  enstrophy_dissipation_hyperviscosity,
  enstrophy_dissipation_hypoviscosity,
  enstrophy_work

using
  CUDA,
  Reexport,
  DocStringExtensions

@reexport using FourierFlows

using LinearAlgebra: mul!, ldiv!
using FourierFlows: parsevalsum

nothingfunction(args...) = nothing

"""
    Problem(dev::Device = CPU();
                     nx = 256,
                     ny = nx,
                     Lx = 2Ï€,
                     Ly = Lx,
                      Î½ = 0,
                     nÎ½ = 1,
                      Î¼ = 0,
                     nÎ¼ = 0,
                     dt = 0.01,
                stepper = "RK4",
                  calcF = nothingfunction,
             stochastic = false,
       aliased_fraction = 1/3,
                      T = Float64)

Construct a two-dimensional Navier-Stokes problem on device `dev`.

Arguments
=========
  - `dev`: (required) `CPU()` or `GPU()`; computer architecture used to time-step `problem`.

Keyword arguments
=================
  - `nx`: Number of grid points in ``x``-domain.
  - `ny`: Number of grid points in ``y``-domain.
  - `Lx`: Extent of the ``x``-domain.
  - `Ly`: Extent of the ``y``-domain.
  - `Î½`: Small-scale (hyper)-viscosity coefficient.
  - `nÎ½`: (Hyper)-viscosity order, `nÎ½```â‰¥ 1``.
  - `Î¼`: Large-scale (hypo)-viscosity coefficient.
  - `nÎ¼`: (Hypo)-viscosity order, `nÎ¼```â‰¤ 0``.
  - `dt`: Time-step.
  - `stepper`: Time-stepping method.
  - `calcF`: Function that calculates the Fourier transform of the forcing, ``FÌ‚``.
  - `stochastic`: `true` or `false`; boolean denoting whether `calcF` is temporally stochastic.
  - `aliased_fraction`: the fraction of high-wavenumbers that are zero-ed out by `dealias!()`.
  - `T`: `Float32` or `Float64`; floating point type used for `problem` data.
"""
function Problem(dev::Device=CPU();
  # Numerical parameters
                nx = 256,
                ny = nx,
                Lx = 2Ï€,
                Ly = Lx,
  # Drag and/or hyper-/hypo-viscosity
                 Î½ = 0,
                nÎ½ = 1,
                 Î¼ = 0,
                nÎ¼ = 0,
  # Timestepper and equation options
                dt = 0.01,
           stepper = "RK4",
             calcF = nothingfunction,
        stochastic = false,
  # Float type and dealiasing
  aliased_fraction = 1/3,
                 T = Float64)

  grid = TwoDGrid(dev; nx, Lx, ny, Ly, aliased_fraction, T)

  params = Params(T(Î½), nÎ½, T(Î¼), nÎ¼, calcF)

  vars = calcF == nothingfunction ? DecayingVars(grid) : (stochastic ? StochasticForcedVars(grid) : ForcedVars(grid))

  equation = Equation(params, grid)

  return FourierFlows.Problem(equation, stepper, dt, grid, vars, params)
end


# ----------
# Parameters
# ----------

"""
    struct Params{T} <: AbstractParams

The parameters for a two-dimensional Navier-Stokes problem:

$(TYPEDFIELDS)
"""
struct Params{T} <: AbstractParams
    "small-scale (hyper)-viscosity coefficient"
       Î½ :: T
    "(hyper)-viscosity order, `nÎ½```â‰¥ 1``"
      nÎ½ :: Int
    "large-scale (hypo)-viscosity coefficient"
       Î¼ :: T
    "(hypo)-viscosity order, `nÎ¼```â‰¤ 0``"
      nÎ¼ :: Int
    "function that calculates the Fourier transform of the forcing, ``FÌ‚``"
  calcF! :: Function
end

Params(Î½, nÎ½) = Params(Î½, nÎ½, typeof(Î½)(0), 0, nothingfunction)


# ---------
# Equations
# ---------

"""
    Equation(params, grid)

Return the `equation` for two-dimensional Navier-Stokes with `params` and `grid`. The linear
operator ``L`` includes (hyper)-viscosity of order ``n_Î½`` with coefficient ``Î½`` and 
hypo-viscocity of order ``n_Î¼`` with coefficient ``Î¼``,

```math
L = - Î½ |ð¤|^{2 n_Î½} - Î¼ |ð¤|^{2 n_Î¼} .
```

Plain-old viscocity corresponds to ``n_Î½ = 1`` while ``n_Î¼ = 0`` corresponds to linear drag.

The nonlinear term is computed via the function `calcN!`.
"""
function Equation(params::Params, grid::AbstractGrid)
  L = @. - params.Î½ * grid.Krsq^params.nÎ½ - params.Î¼ * grid.Krsq^params.nÎ¼
  CUDA.@allowscalar L[1, 1] = 0
  
  return FourierFlows.Equation(L, calcN!, grid)
end


# ----
# Vars
# ----

abstract type TwoDNavierStokesVars <: AbstractVars end

"""
    struct Vars{Aphys, Atrans, F, P} <: TwoDNavierStokesVars

The variables for two-dimensional Navier-Stokes problem:

$(FIELDS)
"""
struct Vars{Aphys, Atrans, F, P} <: TwoDNavierStokesVars
    "relative vorticity"
        Î¶ :: Aphys
    "x-component of velocity"
        u :: Aphys
    "y-component of velocity"
        v :: Aphys
    "Fourier transform of relative vorticity"
       Î¶h :: Atrans
    "Fourier transform of x-component of velocity"
       uh :: Atrans
    "Fourier transform of y-component of velocity"
       vh :: Atrans
    "Fourier transform of forcing"
       Fh :: F
    "`sol` at previous time-step"
  prevsol :: P
end

const DecayingVars = Vars{<:AbstractArray, <:AbstractArray, Nothing, Nothing}
const ForcedVars = Vars{<:AbstractArray, <:AbstractArray, <:AbstractArray, Nothing}
const StochasticForcedVars = Vars{<:AbstractArray, <:AbstractArray, <:AbstractArray, <:AbstractArray}

"""
    DecayingVars(dev, grid)

Return the variables `vars` for unforced two-dimensional Navier-Stokes problem on `grid`.
"""
function DecayingVars(grid::AbstractGrid)
  Dev = typeof(grid.device)
  T = eltype(grid)

  @devzeros Dev T (grid.nx, grid.ny) Î¶ u v
  @devzeros Dev Complex{T} (grid.nkr, grid.nl) Î¶h uh vh
  
  return Vars(Î¶, u, v, Î¶h, uh, vh, nothing, nothing)
end

"""
    ForcedVars(grid)

Return the variables `vars` for forced two-dimensional Navier-Stokes on `grid`.
"""
function ForcedVars(grid::AbstractGrid)
  Dev = typeof(grid.device)
  T = eltype(grid)
  
  @devzeros Dev T (grid.nx, grid.ny) Î¶ u v
  @devzeros Dev Complex{T} (grid.nkr, grid.nl) Î¶h uh vh Fh
  
  return Vars(Î¶, u, v, Î¶h, uh, vh, Fh, nothing)
end

"""
    StochasticForcedVars(grid)

Return the variables `vars` for stochastically forced two-dimensional Navier-Stokes on device `grid`.
"""
function StochasticForcedVars(grid::AbstractGrid)
  Dev = typeof(grid.device)
  T = eltype(grid)

  @devzeros Dev T (grid.nx, grid.ny) Î¶ u v
  @devzeros Dev Complex{T} (grid.nkr, grid.nl) Î¶h uh vh Fh prevsol
  
  return Vars(Î¶, u, v, Î¶h, uh, vh, Fh, prevsol)
end


# -------
# Solvers
# -------

"""
    calcN_advection!(N, sol, t, clock, vars, params, grid)

Calculate the Fourier transform of the advection term, ``- ð–©(Ïˆ, Î¶)`` in conservative form, 
i.e., ``- âˆ‚_x[(âˆ‚_y Ïˆ)Î¶] - âˆ‚_y[(âˆ‚_x Ïˆ)Î¶]`` and store it in `N`:

```math
N = - \\widehat{ð–©(Ïˆ, Î¶)} = - i k_x \\widehat{u Î¶} - i k_y \\widehat{v Î¶} .
```
"""
function calcN_advection!(N, sol, t, clock, vars, params, grid)
  @. vars.uh =   im * grid.l  * grid.invKrsq * sol
  @. vars.vh = - im * grid.kr * grid.invKrsq * sol
  @. vars.Î¶h = sol

  ldiv!(vars.u, grid.rfftplan, vars.uh)
  ldiv!(vars.v, grid.rfftplan, vars.vh)
  ldiv!(vars.Î¶, grid.rfftplan, vars.Î¶h)
  
  uÎ¶ = vars.u                  # use vars.u as scratch variable
  @. uÎ¶ *= vars.Î¶              # u*Î¶
  vÎ¶ = vars.v                  # use vars.v as scratch variable
  @. vÎ¶ *= vars.Î¶              # v*Î¶
  
  uÎ¶h = vars.uh                # use vars.uh as scratch variable
  mul!(uÎ¶h, grid.rfftplan, uÎ¶) # \hat{u*Î¶}
  vÎ¶h = vars.vh                # use vars.vh as scratch variable
  mul!(vÎ¶h, grid.rfftplan, vÎ¶) # \hat{v*Î¶}

  @. N = - im * grid.kr * uÎ¶h - im * grid.l * vÎ¶h
  
  return nothing
end

"""
    calcN!(N, sol, t, clock, vars, params, grid)

Calculate the nonlinear term, that is the advection term and the forcing,

```math
N = - \\widehat{ð–©(Ïˆ, Î¶)} + FÌ‚ .
```
"""
function calcN!(N, sol, t, clock, vars, params, grid)
  dealias!(sol, grid)
  
  calcN_advection!(N, sol, t, clock, vars, params, grid)
  
  addforcing!(N, sol, t, clock, vars, params, grid)
  
  return nothing
end

"""
    addforcing!(N, sol, t, clock, vars, params, grid)

When the problem includes forcing, calculate the forcing term ``FÌ‚`` and add it to the
nonlinear term ``N``.
"""
addforcing!(N, sol, t, clock, vars::DecayingVars, params, grid) = nothing

function addforcing!(N, sol, t, clock, vars::ForcedVars, params, grid)
  params.calcF!(vars.Fh, sol, t, clock, vars, params, grid)
  
  @. N += vars.Fh
  
  return nothing
end
function addforcing!(N, sol, t, clock, vars::StochasticForcedVars, params, grid)
  if t == clock.t # not a substep
    @. vars.prevsol = sol # sol at previous time-step is needed to compute budgets for stochastic forcing
    params.calcF!(vars.Fh, sol, t, clock, vars, params, grid)
  end
  @. N += vars.Fh
  
  return nothing
end


# ----------------
# Helper functions
# ----------------

"""
    updatevars!(prob)

Update problem's variables in `prob.vars` using the state in `prob.sol`.
"""
function updatevars!(prob)
  vars, grid, sol = prob.vars, prob.grid, prob.sol
  
  dealias!(sol, grid)
  
  @. vars.Î¶h = sol
  @. vars.uh =   im * grid.l  * grid.invKrsq * sol
  @. vars.vh = - im * grid.kr * grid.invKrsq * sol
  
  ldiv!(vars.Î¶, grid.rfftplan, deepcopy(vars.Î¶h)) # deepcopy() since inverse real-fft destroys its input
  ldiv!(vars.u, grid.rfftplan, deepcopy(vars.uh)) # deepcopy() since inverse real-fft destroys its input
  ldiv!(vars.v, grid.rfftplan, deepcopy(vars.vh)) # deepcopy() since inverse real-fft destroys its input
  
  return nothing
end

"""
    set_Î¶!(prob, Î¶)

Set the solution `sol` as the transform of `Î¶` and then update variables in `prob.vars`.
"""
function set_Î¶!(prob, Î¶)
  mul!(prob.sol, prob.grid.rfftplan, Î¶)
  
  CUDA.@allowscalar prob.sol[1, 1] = 0 # enforce zero domain average
  
  updatevars!(prob)
  
  return nothing
end

"""
    energy(prob)

Return the domain-averaged kinetic energy. Since ``uÂ² + vÂ² = |{\\bf âˆ‡} Ïˆ|Â²``, the domain-averaged 
kinetic energy is

```math
\\int \\frac1{2} |{\\bf âˆ‡} Ïˆ|Â² \\frac{ð–½x ð–½y}{L_x L_y} = \\sum_{ð¤} \\frac1{2} |ð¤|Â² |ÏˆÌ‚|Â² ,
```

where ``Ïˆ`` is the streamfunction.
"""
@inline function energy(prob)
  sol, vars, grid = prob.sol, prob.vars, prob.grid
  energyh = vars.uh # use vars.uh as scratch variable
  
  @. energyh = 1 / 2 * grid.invKrsq * abs2(sol)
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(energyh, grid)
end

"""
    enstrophy(prob)

Returns the problem's (`prob`) domain-averaged enstrophy,

```math
\\int \\frac1{2} Î¶Â² \\frac{ð–½x ð–½y}{L_x L_y} = \\sum_{ð¤} \\frac1{2} |Î¶Ì‚|Â² ,
```

where ``Î¶`` is the relative vorticity.
"""
@inline enstrophy(prob) = 1 / (2 * prob.grid.Lx * prob.grid.Ly) * parsevalsum(abs2.(prob.sol), prob.grid)

"""
    energy_dissipation(prob, Î¾, Î½Î¾)

Return the domain-averaged energy dissipation rate done by the viscous term,

```math
- Î¾ (-1)^{n_Î¾+1} \\int Ïˆ âˆ‡^{2n_Î¾} Î¶ \\frac{ð–½x ð–½y}{L_x L_y} = - Î¾ \\sum_{ð¤} |ð¤|^{2(n_Î¾-1)} |Î¶Ì‚|Â² ,
```
where ``Î¾`` and ``nÎ¾`` could be either the (hyper)-viscosity coefficient ``Î½`` and its order 
``n_Î½``, or the hypo-viscocity coefficient ``Î¼`` and its order ``n_Î¼``.
"""
@inline function energy_dissipation(prob, Î¾, nÎ¾)
  sol, vars, grid = prob.sol, prob.vars, prob.grid
  energy_dissipationh = vars.uh # use vars.uh as scratch variable
  
  @. energy_dissipationh = - Î¾ * grid.Krsq^(nÎ¾ - 1) * abs2(sol)
  CUDA.@allowscalar energy_dissipationh[1, 1] = 0
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(energy_dissipationh, grid)
end

"""
    energy_dissipation_hyperviscosity(prob)

Return the problem's (`prob`) domain-averaged energy dissipation rate done by the ``Î½`` (hyper)-viscosity.
"""
energy_dissipation_hyperviscosity(prob) = energy_dissipation(prob, prob.params.Î½, prob.params.nÎ½)

"""
    energy_dissipation_hypoviscosity(prob)

Return the problem's (`prob`) domain-averaged energy dissipation rate done by the ``Î¼`` (hypo)-viscosity.
"""
energy_dissipation_hypoviscosity(prob) = energy_dissipation(prob, prob.params.Î¼, prob.params.nÎ¼)

"""
    enstrophy_dissipation(prob, Î¾, Î½Î¾)

Return the problem's (`prob`) domain-averaged enstrophy dissipation rate done by the viscous term,

```math
Î¾ (-1)^{n_Î¾+1} \\int Î¶ âˆ‡^{2n_Î¾} Î¶ \\frac{ð–½x ð–½y}{L_x L_y} = - Î¾ \\sum_{ð¤} |ð¤|^{2n_Î¾} |Î¶Ì‚|Â² ,
```

where ``Î¾`` and ``nÎ¾`` could be either the (hyper)-viscosity coefficient ``Î½`` and its order 
``n_Î½``, or the hypo-viscocity coefficient ``Î¼`` and its order ``n_Î¼``.
"""
@inline function enstrophy_dissipation(prob, Î¾, nÎ¾)
  sol, vars, grid = prob.sol, prob.vars, prob.grid
  enstrophy_dissipationh = vars.uh # use vars.uh as scratch variable
  
  @. enstrophy_dissipationh = - Î¾ * grid.Krsq^nÎ¾ * abs2(sol)
  CUDA.@allowscalar enstrophy_dissipationh[1, 1] = 0
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(enstrophy_dissipationh, grid)
end

"""
    enstrophy_dissipation_hyperviscosity(prob)

Return the problem's (`prob`) domain-averaged enstrophy dissipation rate done by the ``Î½`` (hyper)-viscosity.
"""
enstrophy_dissipation_hyperviscosity(prob) = enstrophy_dissipation(prob, prob.params.Î½, prob.params.nÎ½)

"""
    enstrophy_dissipation_hypoviscosity(prob)

Return the problem's (`prob`) domain-averaged enstrophy dissipation rate done by the ``Î¼`` (hypo)-viscosity.
"""
enstrophy_dissipation_hypoviscosity(prob) = enstrophy_dissipation(prob, prob.params.Î¼, prob.params.nÎ¼)

"""
    energy_work(prob)

Return the problem's (`prob`) domain-averaged rate of work of energy by the forcing ``F``,

```math
- \\int Ïˆ F \\frac{ð–½x ð–½y}{L_x L_y} = - \\sum_{ð¤} ÏˆÌ‚ FÌ‚^* .
```

where ``Ïˆ`` is the stream flow.
"""
@inline energy_work(prob) = energy_work(prob.sol, prob.vars, prob.grid)

@inline function energy_work(sol, vars::ForcedVars, grid)
  energy_workh = vars.uh # use vars.uh as scratch variable
  
  @. energy_workh = grid.invKrsq * sol * conj(vars.Fh)
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(energy_workh, grid)
end

@inline function energy_work(sol, vars::StochasticForcedVars, grid)
  energy_workh = vars.uh # use vars.uh as scratch variable
  
  @. energy_workh = grid.invKrsq * (vars.prevsol + sol) / 2 * conj(vars.Fh)
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(energy_workh, grid)
end

"""
    enstrophy_work(prob)

Return the problem's (`prob`) domain-averaged rate of work of enstrophy by the forcing ``F``,

```math
\\int Î¶ F \\frac{ð–½x ð–½y}{L_x L_y} = \\sum_{ð¤} Î¶Ì‚ FÌ‚^* ,
```

where ``Î¶`` is the relative vorticity.
"""
@inline enstrophy_work(prob) = enstrophy_work(prob.sol, prob.vars, prob.grid)

@inline function enstrophy_work(sol, vars::ForcedVars, grid)
  enstrophy_workh = vars.uh # use vars.uh as scratch variable
  
  @. enstrophy_workh = sol * conj(vars.Fh)
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(enstrophy_workh, grid)
end

@inline function enstrophy_work(sol, vars::StochasticForcedVars, grid)
  enstrophy_workh = vars.uh # use vars.uh as scratch variable

  @. enstrophy_workh = (vars.prevsol + sol) / 2 * conj(vars.Fh)
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(enstrophy_workh, grid)
end

end # module
