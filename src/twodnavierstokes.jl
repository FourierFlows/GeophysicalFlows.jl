module TwoDNavierStokes

export
  Problem,
  set_Î¶!,
  updatevars!,

  energy,
  energy_dissipation,
  energy_work,
  energy_drag,
  enstrophy,
  enstrophy_dissipation,
  enstrophy_work,
  enstrophy_drag

using
  CUDA,
  Reexport

@reexport using FourierFlows

using LinearAlgebra: mul!, ldiv!
using FourierFlows: parsevalsum

nothingfunction(args...) = nothing

"""
    Problem(dev::Device; parameters...)

Construct a 2D Navier-Stokes problem.
"""
function Problem(dev::Device=CPU();
  # Numerical parameters
          nx = 256,
          Lx = 2Ï€,
          ny = nx,
          Ly = Lx,
          dt = 0.01,
  # Drag and/or hyper-/hypo-viscosity
           Î½ = 0,
          nÎ½ = 1,
           Î¼ = 0,
          nÎ¼ = 0,
  # Timestepper and equation options
     stepper = "RK4",
       calcF = nothingfunction,
  stochastic = false,
           T = Float64)

  grid = TwoDGrid(dev, nx, Lx, ny, Ly; T=T)

  params = Params{T}(Î½, nÎ½, Î¼, nÎ¼, calcF)

  vars = calcF == nothingfunction ? Vars(dev, grid) : (stochastic ? StochasticForcedVars(dev, grid) : ForcedVars(dev, grid))

  equation = Equation(params, grid)

  return FourierFlows.Problem(equation, stepper, dt, grid, vars, params, dev)
end


# ----------
# Parameters
# ----------

"""
    Params{T}(Î½, nÎ½, Î¼, nÎ¼, calcF!)

Returns the params for two-dimensional Navier-Stokes.
"""
struct Params{T} <: AbstractParams
    "small-scale (hyper)-viscosity coefficient"
       Î½ :: T
    "(hyper)-viscosity order, `nÎ½ â‰¥ 1`"
      nÎ½ :: Int
    "large-scale (hypo)-viscosity coefficient"
       Î¼ :: T
    "(hypo)-viscosity order, `nÎ½ â‰¤ 0`"
      nÎ¼ :: Int
    "function that calculates the forcing FÌ‚"
  calcF! :: Function  # function that calculates the forcing FÌ‚
end

Params(Î½, nÎ½) = Params(Î½, nÎ½, typeof(Î½)(0), 0, nothingfunction)


# ---------
# Equations
# ---------

"""
    Equation(params, grid)

Returns the `equation` for two-dimensional Navier-Stokes with `params` and `grid`. The linear
opeartor ``L`` includes (hyper)-viscosity of order ``n_Î½`` with coefficient ``Î½`` and 
hypo-viscocity of order ``n_Î¼`` with coefficient ``Î¼``. Plain old viscocity corresponds to 
``n_Î½=1`` while ``n_Î¼=0`` corresponds to linear drag.

```math
L = - Î½ |ð¤|^{2 n_Î½} - Î¼ |ð¤|^{2 n_Î¼} .
```

The nonlinear term is computed via function `calcN!()`.
"""
function Equation(params::Params, grid::AbstractGrid)
  L = @. - params.Î½ * grid.Krsq^params.nÎ½ - params.Î¼ * grid.Krsq^params.nÎ¼
  CUDA.@allowscalar L[1, 1] = 0
  
  return FourierFlows.Equation(L, calcN!, grid)
end


# ----
# Vars
# ----

abstract type TwoDNavierStokesVars <: AbstractVars end

"""
    Vars{Aphys, Atrans, F, P}(Î¶, u, v, Î¶h, uh, vh, Fh, prevsol)

Returns the vars for two-dimensional Navier-Stokes.
"""
struct Vars{Aphys, Atrans, F, P} <: TwoDNavierStokesVars
    "relative vorticity"
        Î¶ :: Aphys
    "x-component of velocity"
        u :: Aphys
    "y-component of velocity"
        v :: Aphys
    "Fourier transform of relative vorticity"
       Î¶h :: Atrans
    "Fourier transform of x-component of velocity"
       uh :: Atrans
    "Fourier transform of y-component of velocity"
       vh :: Atrans
    "Fourier transform of forcing"
       Fh :: F
    "`sol` at previous time-step"
  prevsol :: P
end

const ForcedVars = Vars{<:AbstractArray, <:AbstractArray, <:AbstractArray, Nothing}
const StochasticForcedVars = Vars{<:AbstractArray, <:AbstractArray, <:AbstractArray, <:AbstractArray}

"""
    Vars(dev, grid)

Returns the `vars` for unforced two-dimensional Navier-Stokes problem on device `dev` and 
with `grid`.
"""
function Vars(::Dev, grid::AbstractGrid) where Dev
  T = eltype(grid)
  @devzeros Dev T (grid.nx, grid.ny) Î¶ u v
  @devzeros Dev Complex{T} (grid.nkr, grid.nl) Î¶h uh vh
  return Vars(Î¶, u, v, Î¶h, uh, vh, nothing, nothing)
end

"""
    ForcedVars(dev, grid)

Returns the vars for forced two-dimensional Navier-Stokes on device `dev` and with `grid`.
"""
function ForcedVars(dev::Dev, grid::AbstractGrid) where Dev
  T = eltype(grid)
  @devzeros Dev T (grid.nx, grid.ny) Î¶ u v
  @devzeros Dev Complex{T} (grid.nkr, grid.nl) Î¶h uh vh Fh
  return Vars(Î¶, u, v, Î¶h, uh, vh, Fh, nothing)
end

"""
    StochasticForcedVars(dev, grid)

Returns the vars for stochastically forced two-dimensional Navier-Stokes on device `dev` and 
with `grid`.
"""
function StochasticForcedVars(dev::Dev, grid::AbstractGrid) where Dev
  T = eltype(grid)
  @devzeros Dev T (grid.nx, grid.ny) Î¶ u v
  @devzeros Dev Complex{T} (grid.nkr, grid.nl) Î¶h uh vh Fh prevsol
  return Vars(Î¶, u, v, Î¶h, uh, vh, Fh, prevsol)
end


# -------
# Solvers
# -------

"""
    calcN_advection!(N, sol, t, clock, vars, params, grid)

Calculates the Fourier transform of the advection term, ``- ð–©(Ïˆ, Î¶)`` in conservative 
form, i.e., ``- âˆ‚_y[(âˆ‚_x Ïˆ)Î¶] + âˆ‚_x[(âˆ‚_y Ïˆ)Î¶]`` and stores it in `N`:

```math
N(Î¶Ì‚) = - \\widehat{ð–©(Ïˆ, Î¶)} = - i k_x \\widehat{u Î¶} - i k_y \\widehat{v Î¶} .
```
"""
function calcN_advection!(N, sol, t, clock, vars, params, grid)
  @. vars.uh =   im * grid.l  * grid.invKrsq * sol
  @. vars.vh = - im * grid.kr * grid.invKrsq * sol
  @. vars.Î¶h = sol

  ldiv!(vars.u, grid.rfftplan, vars.uh)
  ldiv!(vars.v, grid.rfftplan, vars.vh)
  ldiv!(vars.Î¶, grid.rfftplan, vars.Î¶h)
  
  uÎ¶ = vars.u                  # use vars.u as scratch variable
  @. uÎ¶ *= vars.Î¶              # u*Î¶
  vÎ¶ = vars.v                  # use vars.v as scratch variable
  @. vÎ¶ *= vars.Î¶              # v*Î¶
  
  uÎ¶h = vars.uh                # use vars.uh as scratch variable
  mul!(uÎ¶h, grid.rfftplan, uÎ¶) # \hat{u*Î¶}
  vÎ¶h = vars.vh                # use vars.vh as scratch variable
  mul!(vÎ¶h, grid.rfftplan, vÎ¶) # \hat{v*Î¶}

  @. N = - im * grid.kr * uÎ¶h - im * grid.l * vÎ¶h
  
  return nothing
end

"""
    calcN!(N, sol, t, clock, vars, params, grid)

Calculates the nonlinear term, that is the advection term and the forcing,

```math
N(Î¶Ì‚) = - \\widehat{ð–©(Ïˆ, Î¶)} + FÌ‚ ,
```

by calling `calcN_advection!` and `addforcing!`.
"""
function calcN!(N, sol, t, clock, vars, params, grid)
  calcN_advection!(N, sol, t, clock, vars, params, grid)
  addforcing!(N, sol, t, clock, vars, params, grid)
  
  return nothing
end

"""
    addforcing!(N, sol, t, clock, vars, params, grid)

When the problem includes forcing, calculate the forcing term ``FÌ‚`` and add it to the 
nonlinear term ``N``.
"""
addforcing!(N, sol, t, clock, vars::Vars, params, grid) = nothing

function addforcing!(N, sol, t, clock, vars::ForcedVars, params, grid)
  params.calcF!(vars.Fh, sol, t, clock, vars, params, grid)
  @. N += vars.Fh
  
  return nothing
end

function addforcing!(N, sol, t, clock, vars::StochasticForcedVars, params, grid)
  if t == clock.t # not a substep
    @. vars.prevsol = sol # sol at previous time-step is needed to compute budgets for stochastic forcing
    params.calcF!(vars.Fh, sol, t, clock, vars, params, grid)
  end
  @. N += vars.Fh
  
  return nothing
end


# ----------------
# Helper functions
# ----------------

"""
    updatevars!(prob)

Update variables in `vars` with solution in `sol`.
"""
function updatevars!(prob)
  vars, grid, sol = prob.vars, prob.grid, prob.sol
  
  @. vars.Î¶h = sol
  @. vars.uh =   im * grid.l  * grid.invKrsq * sol
  @. vars.vh = - im * grid.kr * grid.invKrsq * sol
  
  ldiv!(vars.Î¶, grid.rfftplan, deepcopy(vars.Î¶h))
  ldiv!(vars.u, grid.rfftplan, deepcopy(vars.uh))
  ldiv!(vars.v, grid.rfftplan, deepcopy(vars.vh))
  
  return nothing
end

"""
    set_Î¶!(prob, Î¶)

Set the solution `sol` as the transform of `Î¶` and then update variables in `vars`.
"""
function set_Î¶!(prob, Î¶)
  mul!(prob.sol, prob.grid.rfftplan, Î¶)
  CUDA.@allowscalar prob.sol[1, 1] = 0 # zero domain average
  
  updatevars!(prob)
  
  return nothing
end

# = \\sum_{ð¤} \\frac1{2} |ð¤|^2 |ÏˆÌ‚|^2 .

"""
    energy(prob)

Returns the domain-averaged kinetic energy,
```math
\\int \\frac1{2} (uÂ² + vÂ²) \\frac{ð–½x ð–½y}{L_x L_y} = \\int \\frac1{2} |{\\bf âˆ‡} Ïˆ|Â² \\frac{ð–½x ð–½y}{L_x L_y} = \\sum_{ð¤} \\frac1{2} |ð¤|Â² |ÏˆÌ‚|Â² .
```
"""
@inline function energy(prob)
  sol, vars, grid = prob.sol, prob.vars, prob.grid
  energyh = vars.uh # use vars.uh as scratch variable
  
  @. energyh = 1 / 2 * grid.invKrsq * abs2(sol)
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(energyh, grid)
end

"""
    enstrophy(prob)

Returns the domain-averaged enstrophy,
```math
\\int \\frac1{2} Î¶Â² \\frac{ð–½x ð–½y}{L_x L_y} = \\sum_{ð¤} \\frac1{2} |Î¶Ì‚|Â² .
```
"""
@inline function enstrophy(prob)
  sol, grid = prob.sol, prob.grid
  return 1 / (2 * grid.Lx * grid.Ly) * parsevalsum(abs2.(sol), grid)
end

"""
    energy_dissipation(prob)

Returns the domain-averaged energy dissipation rate done by the ``Î½`` viscous term,
```math
- Î½ (-1)^{n_Î½+1} \\int Ïˆ âˆ‡^{2 n_Î½}Î¶ \\frac{ð–½x ð–½y}{L_x L_y} = - Î½ \\sum_{ð¤} |ð¤|^{2(n_Î½-1)} |Î¶Ì‚|Â² .
```
"""
@inline function energy_dissipation(prob)
  sol, vars, params, grid = prob.sol, prob.vars, prob.params, prob.grid
  energy_dissipationh = vars.uh # use vars.uh as scratch variable
  
  @. energy_dissipationh = - params.Î½ * grid.Krsq^(params.nÎ½ - 1) * abs2(sol)
  CUDA.@allowscalar energy_dissipationh[1, 1] = 0
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(energy_dissipationh, grid)
end

"""
    enstrophy_dissipation(prob)

Returns the domain-averaged enstrophy dissipation rate done by the ``Î½`` viscous term,
```math
Î½ (-1)^{n_Î½+1} \\int Î¶ âˆ‡^{2 n_Î½}Î¶ \\frac{ð–½x ð–½y}{L_x L_y} = - Î½ \\sum_{ð¤} |ð¤|^{2n_Î½} |Î¶Ì‚|Â² .
```
"""
@inline function enstrophy_dissipation(prob)
  sol, vars, params, grid = prob.sol, prob.vars, prob.params, prob.grid
  enstrophy_dissipationh = vars.uh # use vars.uh as scratch variable
  
  @. enstrophy_dissipationh = - params.Î½ * grid.Krsq^params.nÎ½ * abs2(sol)
  CUDA.@allowscalar enstrophy_dissipationh[1, 1] = 0
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(enstrophy_dissipationh, grid)
end

"""
    energy_drag(prob)

Returns the extraction of domain-averaged energy done by the ``Î¼`` viscous term,
```math
- Î¼ (-1)^{n_Î¼+1} \\int Ïˆ âˆ‡^{2 n_Î¼}Î¶ \\frac{ð–½x ð–½y}{L_x L_y} = - Î½ \\sum_{ð¤} |ð¤|^{2(n_Î¼-1)} |Î¶Ì‚|Â² .
```
"""
@inline function energy_drag(prob)
  sol, vars, params, grid = prob.sol, prob.vars, prob.params, prob.grid
  
  energy_dragh = vars.uh # use vars.uh as scratch variable
  
  @. energy_dragh = - params.Î¼ * grid.Krsq^(params.nÎ¼ - 1) * abs2(sol)
  CUDA.@allowscalar energy_dragh[1, 1] = 0
  
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(energy_dragh, grid)
end

"""
    enstrophy_drag(prob)

Returns the extraction of domain-averaged enstrophy by the ``Î¼`` viscous term,
```math
Î¼ (-1)^{n_Î¼+1} \\int Î¶ âˆ‡^{2 n_Î¼}Î¶ \\frac{ð–½x ð–½y}{L_x L_y} = - Î¼ \\sum_{ð¤} |ð¤|^{2n_Î¼} |Î¶Ì‚|Â² .
```
"""
@inline function enstrophy_drag(prob)
  sol, vars, params, grid = prob.sol, prob.vars, prob.params, prob.grid

  enstrophy_dragh = vars.uh # use vars.uh as scratch variable
  
  @. enstrophy_dragh = - params.Î¼ * grid.Krsq^params.nÎ¼ * abs2(sol)
  CUDA.@allowscalar enstrophy_dragh[1, 1] = 0
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(enstrophy_dragh, grid)
end

"""
    energy_work(prob)
    energy_work(sol, vars, grid)

Returns the domain-averaged rate of work of energy by the forcing ``F``,
```math
- \\int Ïˆ F \\frac{ð–½x ð–½y}{L_x L_y} = - \\sum_{ð¤} ÏˆÌ‚ FÌ‚^* .
```
"""
@inline function energy_work(sol, vars::ForcedVars, grid)
  energy_workh = vars.uh # use vars.uh as scratch variable
  
  @. energy_workh = grid.invKrsq * sol * conj(vars.Fh)
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(energy_workh, grid)
end

@inline function energy_work(sol, vars::StochasticForcedVars, grid)
  energy_workh = vars.uh # use vars.uh as scratch variable
  
  @. energy_workh = grid.invKrsq * (vars.prevsol + sol) / 2 * conj(vars.Fh) # Stratonovich
  # @. energy_workh = grid.invKrsq * vars.prevsol * conj(vars.Fh)           # Ito
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(energy_workh, grid)
end

@inline energy_work(prob) = energy_work(prob.sol, prob.vars, prob.grid)

"""
    enstrophy_work(prob)
    enstrophy_work(sol, vars, grid)

Returns the domain-averaged rate of work of enstrophy by the forcing ``F``,
```math
\\int Î¶ F \\frac{ð–½x ð–½y}{L_x L_y} = \\sum_{ð¤} Î¶Ì‚ FÌ‚^* .
```
"""
@inline function enstrophy_work(sol, vars::ForcedVars, grid)
  enstrophy_workh = vars.uh # use vars.uh as scratch variable
  
  @. enstrophy_workh = sol * conj(vars.Fh)
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(enstrophy_workh, grid)
end

@inline function enstrophy_work(sol, vars::StochasticForcedVars, grid)
  enstrophy_workh = vars.uh # use vars.uh as scratch variable
  
  @. enstrophy_workh = (vars.prevsol + sol) / 2 * conj(vars.Fh) # Stratonovich
  # @. enstrophy_workh = grid.invKrsq * vars.prevsol * conj(vars.Fh)           # Ito
  return 1 / (grid.Lx * grid.Ly) * parsevalsum(enstrophy_workh, grid)
end

@inline enstrophy_work(prob) = enstrophy_work(prob.sol, prob.vars, prob.grid)

end # module
